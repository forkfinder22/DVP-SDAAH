title: The Economic Crisis
new-page: true
is-card: true
view-if: crisis_urgency >= 1 and crisis_program_timer = 0 and economic_plan = 0 and black_thursday_seen == 1
frequency: 200
on-arrival: month_actions += 1; crisis_program_timer = 5
card-image: img/bankrun.jpg
tags: party_affairs, govt_affairs

= Addressing the Economic Crisis

Our party is wracked as to how to deal with the economic crisis. From the Nationalist faction, Nationalist Leader argues that the economic crisis must be ignored, as economic crises are simply part of capitalism

From the Industrialists, the inudstrialist leader leader argues that we must pursue balancing the budget and lowering taxes

The Liberals argue for a limited plan of job creation, especially through the military as well as a small amount of public works, and argue that this plan will receive the most support from our democratic allies in the DDP and Z. Liberals also argue that we may even be able to convince the Socialists to adopt this plan, and thereby diffusing a much more left wing plan

Democrats are relatively nuetral as a whole, but many are siding with the Liberal Plan

- @support_nat: Support the Nationalist views.
- @support_indus: Support the Industrial views.
- @support_lib: Support the Liberal and Democratic views.
- @defer: Defer making a decision until we have more information.

@support_nat
subtitle: Let the depression play itself out.
on-arrival: nat_strength += 5; nat_dissent -= 5; lib_dissent += 10; dem_dissent +=5; indus_dissent +=1; lib_strength -=5; dem_strength -=1

The Liberals, many Democrats, and some of the Industrialists criticize our decision, however they cannot do much about our decision

Our policy course is currently that nothing can be should to alleviate the depression.


@support_indus
subtitle: Call for decreased taxes on the rich, and a balanced budget
unavailable-subtitle: The Industrialist faction is not strong enough.
choose-if: historical_mode == 0 or (indus_strength >= lib_strength and indus_strength >= dem_strength/2 and indus_strength >= lib_dissent)
on-arrival: lib_dissent += 5; dem_dissent += 10; indus_strength += 5; richplan_support += 1;
go-to: plan_adopted if richplan_support >= 3; plan_support if richplan_support < 3

@plan_support

Our support of the Industrialist plan alienates the Democratic faction, and faces opposition from the Liberals and Nationalists

We have not yet convinced the party to formally adopt the plan. More work needs to be done.

@plan_adopted
on-arrival: richplan_adopted = 1; indus_dissent -= 30; nat_dissent += 1; indus_strength += 10; lib_strength -= 10; lib_dissent += 10; dem_dissent += 20; dem_strength -= 10; economic_plan = 1

With our hard work, we have finally convinced the majority of the party to adopt the Industrialist plan! [? if dvp_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?]

@support_lib
subtitle: Call for a limited program of job creation and public works
unavailable-subtitle: The Liberal and Democratic factions are not strong enough.
choose-if: historical_mode == 0 or (dem_strength + lib_strength >= indus_strength+nat_strength)
on-arrival: lib_strength += 10; dem_dissent -= 5; indus_dissent += 10; moderate_plan_support += 1
go-to: support_lib_progress if moderate_plan_support < 3; adopt_moderate if moderate_plan_support >= 3

@support_lib_progress

We attempt to convince the party to support a program of limited job creation and public works. Advocates of this plan argue that it will recive the most support from the DDP and Z, and possibly even the SPD

We have not yet formally adopted the plan. More work needs to be done.

@adopt_moderate
on-arrival: reformist_strength += 10; reformist_dissent -= 10; left_dissent += 8; labor_dissent += 6; moderate_plan_adopted = 1; economic_plan = 2; coalition_dissent -= 1 if unemployed > 15; z_relation += 5;

With our hard work, we have convinced the majority of the party to adopt the moderate job creation plan! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?]

@defer

We hold off on making a decision on economic policy, awaiting more information. Woytinsky and his supporters argue that inaction is a form of action of its own. 
